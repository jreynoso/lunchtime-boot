plugins {
    id "org.springframework.boot" version "2.2.6.RELEASE"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id "java"
    id "idea"
    id "groovy"
    id "io.freefair.lombok" version "5.0.0-rc6"
    id "org.ajoberstar.grgit" version "4.0.2"
    id "com.palantir.docker" version "0.25.0"
    id "com.palantir.docker-run" version "0.25.0"
}

group = "com.dispassionproject"
version = "0.0.1-${getBuildNo()}"
sourceCompatibility = "1.8"

repositories {
    mavenCentral()
}

ext {
    google_maps_api_version = "0.11.0"
    groovy_version = "2.5.8"
    guava_version = "29.0-jre"
    javafaker_version = "1.0.2"
    jcabi_manifests_version = "1.1"
    spock_version = "2.0-M1-groovy-2.5"
}

dependencies {
    implementation "com.google.guava:guava:${guava_version}"
    implementation "com.google.maps:google-maps-services:${google_maps_api_version}"
    implementation "com.jcabi:jcabi-manifests:${jcabi_manifests_version}"
    implementation "org.springframework.boot:spring-boot-starter-web"

    testImplementation "com.github.javafaker:javafaker:${javafaker_version}"
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }

    // needed for spock
    implementation "org.codehaus.groovy:groovy-all:${groovy_version}"
    testImplementation platform("org.spockframework:spock-bom:${spock_version}")
    testImplementation "org.spockframework:spock-core"
    testImplementation "org.spockframework:spock-spring"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

bootJar {
    archivesBaseName = project.name
    version = project.version

    manifest {
        attributes "Project-Name": project.name,
                "Version": project.version,
                "Commit": grgit.head() ? grgit.head().abbreviatedId : "N/A",
                "Build-Date": new Date(),
                "Build": getBuildNo()
    }

    exclude "**/application-local.yml"
}

docker {
    name "dispassionproject/${project.name}:${project.version}"
    files tasks.bootJar.outputs
    buildArgs([JAR_FILE: "*.jar"])
}

docker.dependsOn bootJar

dockerRun {
    name "${project.name}"
    image "dispassionproject/${project.name}:${project.version}"
    ports "8080:8080", "5005:5005"
    env "JAVA_OPTIONS": \
        "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005 -Dgoogle.api.key=${getGoogleApiKey()}"
    clean true
}

static def getBuildNo() {
    System.getenv("CIRCLE_BUILD_NUM") ?: "SNAPSHOT"
}

static def getGoogleApiKey() {
    System.getenv("GOOGLE_API_KEY") ?: "N/A"
}
